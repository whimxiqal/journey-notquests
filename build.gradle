plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

defaultTasks 'licenseFormat', 'build'

apply plugin: 'java'
apply plugin: 'org.cadixdev.licenser'

group = 'net.whimxiqal.journey'
version = '1.2.0'


def targetJavaVersion = 17
def javaVersion = JavaVersion.toVersion(targetJavaVersion)
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from '../LICENSE.txt'
    archiveBaseName.set("JourneyNotQuests")
}

license {
    header = rootProject.file('LICENSE.txt')
    include '**/*.java'
    newLine = true
}

repositories {
    mavenCentral()
    maven {
        name = 'paper'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'sonatype-snapshots'
        url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'net.whimxiqal.journey:core-api:1.2-SNAPSHOT'
    compileOnly 'net.whimxiqal.journey:bukkit-api:1.2-SNAPSHOT'
    compileOnly 'com.github.AlessioGr.NotQuests:common:v5.14.0'
    compileOnly 'com.github.AlessioGr.NotQuests:paper:v5.14.0:dev-all'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
